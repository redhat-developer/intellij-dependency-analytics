
plugins {
   id "org.jetbrains.intellij" version "1.15.0"
}

repositories {
    mavenCentral()
	flatDir {
	    dirs buildDir
	}
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://maven.pkg.github.com/RHEcosystemAppEng/exhort-java-api'
        credentials {
            username = project.findProperty("gpr.username") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

// Include the generated files in the source set
sourceSets {
    main {
        java {
            srcDirs("src/main/gen")
        }
    }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = '11'
targetCompatibility = '11'

intellij {
    type = "IC"
    version = ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'org.jboss.tools.intellij.analytics'
    plugins = ['com.redhat.devtools.intellij.telemetry:1.1.0.52',
               "org.jetbrains.plugins.go:223.7571.182",
               "Docker:223.7571.175"]
    updateSinceUntilBuild = false
}

runPluginVerifier {
    ideVersions = [ideaVersion]
}

publishPlugin {
    token    = jetBrainsToken
    channels = [jetBrainsChannel]
}

dependencies {
    configurations.all {
        // this means SNAPSHOT versions will be updated for every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    implementation 'org.kohsuke:github-api:1.314'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'com.redhat.exhort:exhort-java-api:0.0.8-SNAPSHOT'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    // https://mvnrepository.com/artifact/com.github.package-url/packageurl-java
    implementation group: 'com.github.package-url', name: 'packageurl-java', version: '1.4.1'


    testImplementation('junit:junit:4.13.1')
}

runIde {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

runIdeForUiTests {
    systemProperties['com.redhat.devtools.intellij.telemetry.mode'] = 'debug'
}

group 'org.jboss.tools.intellij'
version projectVersion // Plugin version
